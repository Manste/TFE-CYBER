rule "Handle Recognized Person Event"
when
    Channel "mqtt:topic:mqttHome:mqttHomeTopic:DetectedPersonEvent" triggered
then
    val personsListString = receivedEvent
    logInfo("FaceRecognition", "Recognized Persons: " + personsListString)

    val unauthorizedPersons = newArrayList("Unknown", "Manuelle")
    var unauthorized = false
    var trimmedPerson = ""

    try {
        // Split the received persons list string (assuming it's like ["Unknown"])
        val personList = personsListString.replace("[", "").replace("]", "").replace("\"", "").split(",")
        //logInfo("FaceRecognition", "Parsed Persons: " + personList.toString()) //for debugging

        // Check if any unauthorized person is detected
        for (person : personList) {
            trimmedPerson = person.trim
            if (unauthorizedPersons.contains(trimmedPerson)) {
                logWarn("FaceRecognition", "Unauthorized person detected: " + trimmedPerson)
                unauthorized = true
            }
        }

        if (unauthorized) {
            // Trigger an alarm
            executeCommandLine("python3", "/etc/openhab/scripts/alarm.py", trimmedPerson, "&")
        }
    } catch (Exception e) {
        logError("FaceRecognition", "Error parsing JSON or executing command: " + e.message)
    }
end
